[
    /* quantification */

    { rule: qp(E1) -> 'every',                                            sense: all(E1) }

    /* noun */

    { rule: proper_noun_group(N1) -> proper_noun(N1) proper_noun(N1) proper_noun(N1)}
    { rule: proper_noun_group(N1) -> proper_noun(N1) proper_noun(N1)}
    { rule: proper_noun_group(N1) -> proper_noun(N1)}

    { rule: nbar(E1) -> noun(E1) }

    { rule: noun(E1) -> parent(E1),                                 sense: parent(E1, _) }

    { rule: np(R1) -> qp(Q1) nbar(R1),                              sense: quant(Q1, sem(1), R1, sem(2), sem(parent)) }
    { rule: np(E1) -> proper_noun_group(E1) }

    { rule: sibling(E1) -> 'siblings' }
    { rule: child(E1) -> 'children' }
    { rule: parent(E1) -> 'parent' }

    /* verb */

    { rule: aux_do(P1) -> 'did' }
    { rule: aux_do(P1) -> 'does' }
    { rule: have(P1) -> 'have' }
    { rule: have(P1) -> 'has' }
    { rule: copula(P1) -> 'are' }
    { rule: marry(P1) -> 'marry' }
    { rule: marry(P1) -> 'married' }

    /* misc */

    { rule: number(N1) -> /^[0-9]+/ }

    /* sentence */

    { rule: which_clause(E1) -> child(E1) have(P1) np(E2),                    sense: have_child(E2, E1) }
    { rule: how_many_clause(E1) -> child(E1) have(P1) np(E2),                 sense: have_child(E2, E1) }
    { rule: subject_complement_clause(E1) -> marry(P1) np(E2),                sense: marry(P1, E1, E2) }
    { rule: subject_complement_clause2(P1) -> np(E1) marry(P1) np(E2),        sense: marry(P1, E1, E2) }
    { rule: subject_complement_clause2(P1) -> np(E1) have(_) number(N2) child(E2),      sense: have_child(E1, E2) number_of(E2, N2) }
    { rule: copula_clause(P1) -> np(E1) 'and' np(E2) sibling(_),              sense: siblings(E1, E2) }

    { rule: interrogative(P1) -> 'who' subject_complement_clause(E1),         sense: who(E1) }
    { rule: interrogative(P1) -> 'which' which_clause(E1),                    sense: which(E1) }
    { rule: interrogative(P1) -> 'how' 'many' how_many_clause(E1),            sense: how_many(E1) }
    { rule: interrogative(P1) -> aux_do(_) subject_complement_clause2(P1),    sense: yes_no(_) }
    { rule: interrogative(P1) -> copula(_) copula_clause(P1),                 sense: yes_no(_) }

    { rule: interrogative(S1) -> interrogative(S1) '?' }
    { rule: s(S1) -> interrogative(S1),                                       sense: question(S1) }
]